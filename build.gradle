//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
plugins {
    id "maven-publish"
    alias libs.plugins.loom
}

version = "$project.baseVersion+$project.branch"
archivesBaseName = "scatteredshards"
group = "net.modfest"

repositories {
    maven { url "https://server.bbkr.space/artifactory/libs-release" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

dependencies {
    minecraft libs.mc
    mappings variantOf(libs.yarn) { classifier "v2" }
    modImplementation libs.fl
    modImplementation libs.fapi
    modCompileOnly libs.libgui
    modImplementation libs.fpapi

    include libs.libgui
    include libs.fpapi
}

processResources {
    final Map<String, String> map = [
            "version": version.toString(),
            "mc"     : libs.versions.mc.get(),
            "fl"     : libs.versions.fl.get(),
            "fapi"   : libs.versions.fapi.get(),
            "libgui" : libs.versions.libgui.get(),
            "fpapi"  : libs.versions.fpapi.get()
    ]

    inputs.properties(map)
    filesMatching('*.mod.json') { expand(map) }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

java {
    withSourcesJar()
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from "LICENSE"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
